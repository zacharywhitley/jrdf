Package org.jrdf.sparql.parser;

/*
 * SPARQL Query Language for RDF SableCC grammar.
 * $Id$
 */

Helpers
  character  = [0..0xFFFF];
  letter     = ['a'..'z'] | ['A'..'Z'];
  digit      = ['0'..'9'];
  tab        = 9;
  lf         = 10;
  cr         = 13;
  sl         = 47;
  whitespace = ' ' | tab | lf | cr;
  nonblank   = [33..126];
  nonquote   = [character - [39 + '\']];
  nonslash   = [[33..126] - sl];
  urlchar    = [character - '>'];

States
  def,  /* default state */
  com,  /* comment */
  esc,  /* escaping character in quoted literal with '\' */
  lit,  /* quoted literal */
  res,  /* tagged resource */
  var;  /* escaping variable name with '?' or '$' */

Tokens
  {def}                select      = ('SELECT' | 'select');
  {def}                where       = ('WHERE' | 'where');
  {def}                union       = ('UNION' | 'union');
  {def}                optional       = ('OPTIONAL' | 'optional');

  {def, var->def}      identifier = letter (letter | digit | '_' | '$' | '?' )*;
  {def}                blank          = whitespace+;
  {def->com}           hash           = '#';
  {com}                comment        = [character - [lf + cr]];
  {com->def}           endcomment     = (lf | cr)+;
  {def}                terminator     = ';';
  {def}                lpar           = '{';
  {def}                rpar           = '}';
  {def->res}           url            = '<';
  {res->def}           endurl         = '>';
  {def}                datatypeprefix = '^^';
  {def->var}           variableprefix = ('?' | '$');
  {def}                period         = '.';
  {def}                number         = digit+;
  {def}                float          = digit+ ('.' digit*) | ('.' digit+);
  {def}                asterisk       = '*';

  {res}                resource    = urlchar*;
  {def->lit, lit->def} quote       = 39;  /* ' */
  {lit->esc}           escape      = '\';
  {esc->lit}           escapedtext = character;
  {lit}                text        = nonquote+;

Ignored Tokens
  blank, comment, escape, endcomment, endurl, hash, url;

Productions
  start =
    {query} select_clause where_clause?;

  select_clause =
    {variable_list} select variable+ |
    {wildcard} select asterisk ;

  where_clause =
    where? graph_pattern;

  graph_pattern =
    {a_basic_graph_pattern} basic_graph_pattern |
    {operation} operation ;

  operation =
    {a_union_graph_pattern} union_graph_pattern |
    {a_optional_graph_pattern} optional_graph_pattern ;

  union_graph_pattern =
    lpar [lhs_graph_pattern]:graph_pattern union [rhs_graph_pattern]:graph_pattern rpar ;

  optional_graph_pattern =
    {optional_with_two_patterns} [lp1]:lpar [lhs_graph_pattern]:pattern_elements_list optional [lp2]:lpar [rhs_graph_pattern]:pattern_elements_list [rp2]:rpar [rp1]:rpar |
    {optional_with_operation} [lp1]:lpar operation optional [lp2]:lpar pattern_elements_list [rp2]:rpar [rp1]:rpar ;

  /* TODO: Don't allow mixes of { and ( parans, e.g. "{ ... )" */
  basic_graph_pattern =
    lpar pattern_elements_list rpar ;

  pattern_elements_list =
    {a_pattern_element} pattern_element |
    {a_grouped_pattern_element} basic_graph_pattern |
    {a_pattern_list} pattern_element period pattern_elements_list? |
    {a_pattern_with_operation} pattern_element period operation ;
    {a_pattern_with_operation} operation period operation ;

  pattern_element =
    triple ;

  /* TODO: Disallow literals as subjects & predicates in the grammar. */
  triple =
    [subject]:resource_triple_element [predicate]:resource_triple_element [object]:object_triple_element ;

  resource_triple_element =
    {resource} resource |
    {variable} variable ;

  object_triple_element =
    {resource} resource |
    {literal}  literal  |
    {variable} variable ;

  literal =
    [opening]:quote strand* [closing]:quote;  /*datatype?;*/

  strand =
    {unescaped} text |
    {escaped}   escapedtext;

  datatype =
    datatypeprefix resource;

  variable =
    variableprefix identifier;

